import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'net.neoforged.gradle' version '[6.0.18,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'

    id 'idea'

    id 'maven-publish'
    id "com.modrinth.minotaur" version "2.+"
    id 'net.darkhax.curseforgegradle' version '1.1.16'
}


group = mod_group_id
version = "${minecraft_version}-${mod_version}"

base {
    archivesName = mod_id
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(17)

    withJavadocJar()
    withSourcesJar()
}

minecraft {
    mappings channel: mapping_channel, version: "${parchment_version}-${minecraft_version}"

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    copyIdeResources = true

    runs {
        // applies to all the run configs below
        configureEach {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            // example of overriding the workingDirectory set in configureEach above
            workingDirectory project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

/* -------------------------------------------- */
/*  Dependencies                                */
/* -------------------------------------------- */

repositories {
    maven { url "https://libraries.minecraft.net" }
    maven { url "https://maven.datdeveloper.com/releases" }
    maven { url "https://cubicinterpolation.net/maven/" }
}

dependencies {
    minecraft "net.neoforged:forge:${minecraft_version}-${neo_version}"

    implementation "com.mojang:brigadier:${brigadier_version}"
    implementation "com.google.code.gson:gson:${gson_version}"

    implementation fg.deobf("com.datdeveloper:datmoddingapi:${datmoddingapi_version}")
    implementation fg.deobf("commoble.infiniverse:infiniverse-${minecraft_version}:${infiniverse_version}")
}

/* -------------------------------------------- */
/*  Tasks                                       */
/* -------------------------------------------- */

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            neo_version: neo_version, neo_version_range: neo_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description,
    ]

    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

// Example for how to get properties into the manifest for reading at runtime.
jar {
    manifest {
        attributes([
                "Specification-Title": mod_id,
                "Specification-Vendor": mod_authors,
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": project.jar.archiveVersion,
                "Implementation-Vendor": mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    finalizedBy('reobfJar')
}

tasks.register('printVersion') {
    doLast {
        println version
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

/* -------------------------------------------- */
/*  Publishing                                  */
/* -------------------------------------------- */

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java

            pom {
                name = project.name
                description = 'A mod to add player run factions into the game'
                url = 'https://datdeveloper.com/factions-2'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/jtljac/factions-2/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'jtljac'
                        name = 'Jacob Lucas'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com.com/jtljac/factions-2.git'
                    developerConnection = 'scm:git:ssh://github.com/jtljac/factions-2.git'
                    url = 'https://factions.datdeveloper.com/'
                }

                withXml {
                    asNode().dependencies.dependency.each { dep ->
                        if(dep.artifactId.last().value().last() == "forge") {
                            assert dep.parent().remove(dep)
                        }
                    }
                }
            }
        }
    }

    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            name = "Dat-Maven-Repo"
            url = "https://maven.datdeveloper.com/releases"
            credentials {
                username = System.getenv("MAVENREPOUSER")
                password = System.getenv("MAVENREPOPASS")
            }
        }
    }
}

// Curse Publishing
tasks.register('publishCurseForge', TaskPublishCurseForge) {
    dependsOn(tasks.jar)
    group = "publishing"

    apiToken=System.getenv("CURSEFORGE_TOKEN")
    def mainFile=upload(curse_id, jar)
    mainFile.releaseType='release'
    mainFile.changelog=System.getenv("CHANGELOG")
    mainFile.changelogType='markdown'
    mainFile.displayName="${mod_name} - ${version}"
    mainFile.addModLoader("NeoForge")
    mainFile.addGameVersion(minecraft_version)
    mainFile.addJavaVersion("Java 17")
}

// Modrinth Publishing
modrinth {
    token=System.getenv("MODRINTH_TOKEN")
    projectId=modrinth_id
    versionName="${mod_name} - ${version}"
    uploadFile=jar
    gameVersions=[minecraft_version]
    changelog=System.getenv("CHANGELOG")
}

// Dependencies
tasks.modrinth.configure {
    dependsOn(tasks.jar)
}

publish.configure {
    dependsOn(tasks.modrinth, tasks.publishCurseForge)
}

tasks.withType(GenerateModuleMetadata).configureEach {
    enabled = false
}